//file:noinspection GroovyAssignabilityCheck

plugins {
    id("java")
    id("checkstyle")
    id("com.github.spotbugs").version("6.0.17")
    id("org.cadixdev.licenser").version("0.6.1")
    id("com.gradleup.shadow").version("8.3.5")
}

setGroup("net.elytrium")
setVersion("1.0.4")

compileJava {
    options.encoding = "UTF-8"
}

java {
    setSourceCompatibility(JavaVersion.VERSION_21)
    setTargetCompatibility(JavaVersion.VERSION_21)
}

repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }
    maven {
        name = "elytrium"
        url = uri("https://maven.elytrium.net/repo/")
    }
    maven() {
        setName("sonatype-snapshots-repo")
        setUrl("https://s01.oss.sonatype.org/content/repositories/snapshots/")
    }
}

dependencies {
    compileOnly(files("lib/limboapi-1.1.27-SNAPSHOT-jan7.jar"))
    compileOnly("net.elytrium.commons:config:$elytriumCommonsVersion")
    compileOnly("net.elytrium.commons:utils:$elytriumCommonsVersion")
    compileOnly("net.elytrium.commons:velocity:$elytriumCommonsVersion")
    compileOnly("net.elytrium.commons:kyori:$elytriumCommonsVersion")
    compileOnly("net.kyori:adventure-nbt:4.17.0")

    compileOnly("com.velocitypowered:velocity-api:$velocityVersion")
    annotationProcessor("com.velocitypowered:velocity-api:$velocityVersion")
    compileOnly("com.velocitypowered:velocity-proxy:$velocityVersion")

    compileOnly("io.netty:netty-codec:$nettyVersion")
    compileOnly("it.unimi.dsi:fastutil-core:$fastutilVersion")

    compileOnly("com.github.spotbugs:spotbugs-annotations:$spotbugsVersion")

    implementation("org.bstats:bstats-velocity:$bstatsVersion")
}

shadowJar {
    archiveClassifier.set("")

    relocate("org.bstats", "net.elytrium.limbohub.thirdparty.org.bstats")
    relocate("net.elytrium.commons.velocity", "net.elytrium.limboapi.thirdparty.commons.velocity")
    relocate("net.elytrium.commons.kyori", "net.elytrium.limboapi.thirdparty.commons.kyori")
    relocate("net.elytrium.commons.config", "net.elytrium.limboapi.thirdparty.commons.config")
}

license {
    header = file("HEADER.txt")
}

checkstyle {
    toolVersion = "10.1"
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    configProperties = ["configDirectory": "${rootDir}/config/checkstyle"]

    // The build should immediately fail if we have errors.
    maxErrors = 0
    maxWarnings = 0
}

spotbugsMain {
    excludeFilter = file("${rootDir}/config/spotbugs/suppressions.xml")

    reports {
        html {
            required = true
            outputLocation = layout.buildDirectory.file("reports/spotbugs/main/spotbugs.html").get().asFile
            stylesheet = "fancy-hist.xsl"
        }
    }
}

def generateTemplates = tasks.register("generateTemplates", Copy) {
    String version = project.version.contains("-") ? "${project.version} (git-${getCurrentShortRevision()})" : project.version
    inputs.property("version", version)
    from(file("src/main/templates"))
    into(layout.buildDirectory.dir("generated/sources/templates"))
    expand("version": version)
}
sourceSets.main.java.srcDir(generateTemplates.map { it.destinationDir })

assemble.dependsOn(shadowJar)

String getCurrentShortRevision() {
    OutputStream outputStream = new ByteArrayOutputStream()
    exec {
        if (System.getProperty("os.name").toLowerCase().contains("win")) {
            commandLine("cmd", "/c", "git rev-parse --short HEAD")
        } else {
            commandLine("bash", "-c", "git rev-parse --short HEAD")
        }

        setStandardOutput(outputStream)
    }

    return outputStream.toString().trim()
}